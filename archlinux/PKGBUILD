_pkgname=go-suspend-on-idle
pkgname=$_pkgname-git
pkgdesc='Go Suspend on Idle'
pkgver=r9.dfbe818
pkgrel=1
depends=('systemd-libs')
makedepends=('cargo')
arch=('x86_64')
license=('MIT')
url='https://github.com/sateffen/go-suspend-on-idle'
source=('git+https://github.com/sateffen/go-suspend-on-idle.git')
sha256sums=('SKIP')

pkgver() {
  # see https://wiki.archlinux.org/title/VCS_package_guidelines#The_pkgver()_function
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  export RUSTUP_TOOLCHAIN=stable
  cd "$_pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  cd "$_pkgname"
  cargo build --frozen --release
}

package() {
  install -Dm755 "$srcdir/$_pkgname/target/release/$_pkgname" "$pkgdir/usr/bin/go-suspend-on-idle"
  install -Dm644 "$srcdir/$_pkgname/go-suspend-on-idle.service" "$pkgdir/usr/lib/systemd/system/go-suspend-on-idle.service"
  install -Dm644 "$srcdir/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
